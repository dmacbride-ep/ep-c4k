// this bit is just plain Jenkins Groovy
def secretsDirectory = new File("/secret/jenkins-secrets").exists() ? new File("/secret/jenkins-secrets") : new File("/var/jenkins_secrets");
def randomUUID = "${UUID.randomUUID().toString()}"
def randomLabel = "jenkins-worker-${randomUUID}"
def dockerRegistryAddress = new File(secretsDirectory, "dockerRegistryAddress").text.trim();
def jenkinsAgentImageTag = new File(secretsDirectory, "cloudOpsForKubernetesDefaultBranch").text.trim().replace('/','-');

// this is a workaround until this bug is fixed: https://issues.jenkins-ci.org/browse/JENKINS-56943
def podYamlFromFile = new File("${env.JENKINS_HOME}/workspace/build-data-pop@script/cloudops-for-kubernetes/jenkins/agents/kubernetes/docker-4gb-2core-1container.yaml").text.trim();
String podYaml = podYamlFromFile.replace('${dockerRegistryAddress}', "${dockerRegistryAddress}").replace('${jenkinsAgentImageTag}', "${jenkinsAgentImageTag}")

def dockerRepoUrl = new File(secretsDirectory, "dockerRepoURL").text.trim()
def commerceRepoUrl = new File(secretsDirectory, "epCommerceRepoURL").text.trim()
def gitCredentialId = new File(secretsDirectory, "gitCredentialId").text.trim()

// this is the Pipeline DSL.
// Syntax here: https://jenkins.io/doc/book/pipeline/syntax/
// More useful docs: https://jenkins.io/doc/pipeline/steps/
pipeline {
  agent {
    kubernetes {
      label "${randomLabel}-1"
      defaultContainer "jnlp"
      yaml "${podYaml}"
    }
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '30', artifactNumToKeepStr: '10000'))
    timeout(time: 90, unit: 'MINUTES')
  }
  stages {
    stage('Validate parameters and login to Docker registry') {
      steps {
        container('docker') {
          script {
            eplib = load "cloudops-for-kubernetes/lib/eplib.groovy"
            buildlib = load "cloudops-for-kubernetes/lib/buildlib.groovy"

            buildlib.validateDockerTag(params.imageTag, "imageTag")

            eplib.loginToCloud()
            eplib.loginToDockerRegistry()
          }
        }
      }
    }
    stage('Checkout the EP Docker code') {
      steps {
        container('docker') {
          dir ('docker') {
            script {
              eplib.gitShallowClone(dockerRepoUrl, "${params.dockerBranch}", gitCredentialId);
            }
          }
        }
      }
    }
    stage('Download Deployment Package') {
      steps {
        container('docker') {
          script {
            if ( ! params.deploymentPackageUrl && params.epCommerceBranch ) {
              buildlib.downloadDeploymentPackageFromJenkins(commerceRepoUrl,"${params.epCommerceBranch}");
            } else if (params.deploymentPackageUrl) {
              buildlib.downloadDeploymentPackageFromUrl("${params.deploymentPackageUrl}");
            } else {
              throw new Exception("you must set one of epCommerceBranch or deploymentPackageUrl")
            }
          }
        }
      }
    }
    stage('Build and push the data-pop-tool image') {
      steps {
        container('docker') {
          script {
            sh """
            cd docker/data-pop-tool/
            mv /root/deploymentpackage.zip ./ext-deployment-package-0.zip
            img build -t "${dockerRegistryAddress}/ep/data-pop-tool:${params.imageTag}" .
            img push "${dockerRegistryAddress}/ep/data-pop-tool:${params.imageTag}"
            rm ./ext-deployment-package-0.zip
            """
          }
        }
      }
    }
  }
}
